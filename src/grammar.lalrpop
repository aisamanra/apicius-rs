use crate::types::*;

grammar(state: &mut State);

pub Recipe: Recipe = {
    <name:Text> "{" <rules:(<Rule> ";")*> "}" => Recipe { name, rules },
};

Loc<T>: Loc<T> = {
  <l:@L> <value:T> <r:@R> => Loc { l, r, value },
};

Separated<T, Sep>: Vec<T> = {
    <vs:Separated<T, Sep>> <sep:Sep> <v:T> => {
        let mut vs = vs;
        vs.push(v);
        vs
    },
    <v: T> => vec![v],
};

Rule: RuleRef = {
    <inp: Input> "->" <actions: Separated<Action, "->">> =>
        state.add_rule(Rule { input: inp, actions }),
};

Action: Action = {
    "<>" => Action::Done,
    <point:JoinPoint> => Action::Join { point },
    <action:Text> => Action::Action { step: ActionStep { action, seasonings: vec![] } },
    <action:Text> "&" <seasonings:IngredientList> => Action::Action { step: ActionStep { action, seasonings } },
};

Input: Input = {
    <point:JoinPoint> => Input::Join { point },
    <list:IngredientList> => Input::Ingredients { list },
};

IngredientList: Vec<IngredientRef> = Separated<Ingredient, "+">;

Ingredient: IngredientRef = {
    "[" <amount:Text> "]" <stuff:Text> =>
        state.add_ingredient(Ingredient {
            amount: Some(amount),
            stuff
        }),
    <stuff:Text> =>
        state.add_ingredient(Ingredient {
            amount: None,
            stuff
        }),
};

Text: StringRef = {
  <l:@L> <s:r"[/A-Za-z0-9,_ ]*"> <r:@R> => {
      let value = state.add_string(s.trim());
      Loc { l, r, value }
  }
};

JoinPoint: StringRef =
    <l:@L> <s:r"\$[A-Za-z0-9_ ]*"> <r:@R> => {
        let value = state.add_string(s.trim());
        Loc { l, r, value }
    };
